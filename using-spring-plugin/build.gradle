plugins {
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'java'
    id 'project-report'
}

def dependencyVersionsPath = '../dependency-versions/versions.properties'
ext {
    versions = new Properties()
    file(dependencyVersionsPath).withInputStream versions.&load
}

repositories {
    jcenter()
}

dependencyManagement {
    dependencies {
        versions.each { key, value ->
            def keyParts = key.split ':'
            switch (keyParts.length) {
                case 2:
                    dependency group: keyParts[0], name: keyParts[1], version: value
                    break
                default:
                    def msg = "Illegal property name found in '$dependencyVersionsPath': $key"
                    throw new InvalidUserDataException(msg)
            }
        }
    }
}

dependencies {
    // The versions of transitive deps (eg. jackson-annotations and jackson-core)
    //   are based on those from the properties file (as we're using a custom resolution strategy)
    compile 'com.fasterxml.jackson.core:jackson-databind'

    // If for some reason you DON'T want to use the version specified in the properties file
    //   for a specific dependency
    compile 'commons-io:commons-io:2.5'

    compile 'org.apache.camel:camel-jms'
    compile 'org.springframework:spring-aop'
}
