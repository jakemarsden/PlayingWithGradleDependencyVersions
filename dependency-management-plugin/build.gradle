plugins {
    id 'groovy'
    id 'maven-publish'
}

group = 'com.jakemarsden.gradleplugins'
version = '1.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

sourceSets {
    main.java { srcDirs = [] }
    test.java { srcDirs = [] }
}

groovydoc {
    includePrivate true
}

task groovydocJar(type: Jar) {
    classifier 'javadoc'
    from groovydoc.destinationDir
    dependsOn groovydoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

build {
    dependsOn groovydocJar
    dependsOn sourcesJar
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact groovydocJar
            artifact sourcesJar
        }
    }
}

task install {
    Task delegate = publishToMavenLocal
    group delegate.group
    description delegate.description
    dependsOn delegate
}
